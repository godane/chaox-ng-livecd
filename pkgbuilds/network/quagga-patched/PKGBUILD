#
# quagga routing suite PKGBUILD
# Forked from quagga AUR by helios <helios@cyb0rg.org>
#
# Thanks to quagga AUR Maintainer:
# Contributor: shild <shildv@gmail.com> 
# Contributor: Georg Grabler (STiAT) <ggrabler@gmail.com>
# Contributor: Bart Kos, bro at fast-stable-secure dot net
# Contributor: HomeCreate <homecreate@list.ru>
#
# And equinox <dl@diac24.net>

pkgname=quagga-patched
pkgver=20090902
pkgrel=1
pkgdesc="Advanced routing protocol suite. With AS4, advanced dumpfile output and IPv6 linklocal peering support"
arch=('i686' 'x86_64')
url="http://www.quagga.net"
license=("GPL2")
makedepends=('git')
depends=('libcap' 'libnl' 'net-snmp' 'readline' 'iproute' 'cpulimit')
backup=('etc/quagga.d/zebra.conf' 'etc/quagga.d/ripd.conf' 'etc/quagga.d/ripng.conf' 'etc/quagga.d/ospfd.conf' 'etc/quagga.d/ospf6d.conf' 'etc/quagga.d/bgpd.conf' 'etc/conf.d/quagga')
install=quagga.install
provides=('quagga')
conflicts=('quagga')
_gitroot='git://git.spaceboyz.net/equinox/quagga.git'
_gitname='quagga'

source=(zebra ripd ripngd ospfd ospf6d bgpd quagga.conf)

md5sums=('91c4aa01a2e65b49813c92b1b8fa6b2f'
         '6177340646f2c5a5547ecee1473ebfb9'
         '30fb1c5525eaa15a9a90e9784b4e468d'
         '490af652d2d853cb4eeb64feca98bc5a'
         '9eee3e9af74150ceb6bbbcadf8df8eb6'
         '231ff5417e7c5e82b4d82a90560214e5'
         '1fd63dc935eece30d871c7d007b1c432')
build() {
    cd "${srcdir}"
    if [ -e quagga ] ; then
      cd quagga
      git pull . master || return 1
      cd ..
    else
      git clone $_gitroot $_gitname || return 1
      cd quagga
      git checkout -t -l -b equinox origin/equinox
      cd ..
    fi
    cd "${srcdir}"/quagga
    ./bootstrap.sh
    ./configure --prefix=/usr \
		--sysconfdir=/etc/quagga.d \
		--localstatedir=/var/lib/quagga \
		--enable-snmp \
		--enable-vtysh \
		--enable-netlink \
		--enable-tcp-zebra \
		--enable-opaque-lsa \
		--enable-ospf-te \
		--enable-multipath=0 \
		--enable-user=quagga \
		--enable-group=quagga \
		--enable-rtadv || return 1
    
    make || return 1
    make DESTDIR="${pkgdir}" PREFIX="${pkgdir}" install || return 1

    for line in bgpd ospfd ospf6d zebra ripd ripngd
    do
	install -D -m 755 -o root -g root "${srcdir}"/$line "${pkgdir}"/etc/rc.d/$line || return 1
	
	touch "${pkgdir}"/etc/quagga.d/$line.conf || return 1
	echo "password quagga" > "${pkgdir}"/etc/quagga.d/$line.conf || return 1
    done

    mkdir -p "${pkgdir}"/etc/conf.d || return 1
    install -D -m 644 -o root -g root "${srcdir}"/quagga.conf "${pkgdir}"/etc/conf.d/quagga || return 1

    chmod 770 "${pkgdir}"/etc/quagga.d || return 1
    rm "${pkgdir}"/usr/share/info/dir || return 1
}
