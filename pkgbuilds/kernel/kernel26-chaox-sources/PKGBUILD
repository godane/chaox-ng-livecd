# Contributor: Jens Pranaitis <jens@jenux.homelinux.org>
# Based on the work of godane <slaxemulator@gmail.com>
pkgname=kernel26-chaox-sources
_basekernel=2.6.29
pkgver=2.6.29.2
pkgrel=1
pkgdesc="Linux kernel based on slax config. Used in chaox-ng"
arch=("i686")
license=('GPL2')
groups=('base')
url="http://www.kernel.org"
makedepends=("git")
depends=('coreutils' 'module-init-tools' 'mkinitcpio>=0.5.18')
source=(ftp://ftp.kernel.org/pub/linux/kernel/v2.6/linux-$_basekernel.tar.bz2
        http://kernel.org/pub/linux/kernel/v2.6/patch-$pkgver.bz2
        config
        http://patches.aircrack-ng.org/mac80211_2.6.28-rc4-wl_frag+ack_v3.patch
        #ath5k patches stolen from pentoo ;)
        https://trac.pentoo.ch/pentoo/export/763/kernel/trunk/2.6/2.6.28/4001_ath5k-injection-2.6.28.patch
        https://trac.pentoo.ch/pentoo/export/763/kernel/trunk/2.6/2.6.28/4007_ath5k-pass-failed-crc.patch
        https://trac.pentoo.ch/pentoo/export/763/kernel/trunk/2.6/2.6.28/4008_ath5k-frequency-chaos-2.6.28.patch
        openlog.patch
        http://patches.aircrack-ng.org/zd1211rw_inject_2.6.26.patch
        http://patches.aircrack-ng.org/mac80211-2.6.29-fix-tx-ctl-no-ack-retry-count.patch
        http://patches.aircrack-ng.org/rtl8187-mac80211-injection-speed-2.6.28-wl.patch
        squashfs3.4-include.patch
        ftp://kernel.org/pub/linux/kernel/people/edward/reiser4/reiser4-for-2.6/reiser4-for-2.6.29.patch.gz
)
md5sums=('64921b5ff5cdadbccfcd3820f03be7d8'
         'a3e79545ef18ff2a172f82fffef92deb'
         'f9561cf90f0d7567e966f66d15e55fde'
         'ca85d39144f07b0500c663779dda9259'
         '80b536cef263991fa7fbaee217fa9191'
         '3826efcfe08f1cbf978c02bd2f3c37b1'
         '031cce31cd2dc8010ee27f5898853355'
         'a49390dd08e4f7702b8f4b550f1ad84e'
         'a162a23b5db02738309e54b6ca9fcbc9'
         'a713e50e8f2cf82ae9cf7e10a9f82412'
         '12e8d4bf7b430ce3cc6656c24e298a33'
         '2ed00248c56672a6664864084011e85a'
         'ace47269d99bb5e555e01417f3337cd8')
build() {
  KARCH=x86
  cd "${srcdir}"
  git clone http://git.c3sl.ufpr.br/pub/scm/aufs/aufs2-standalone.git
  cd aufs2-standalone
  git checkout origin/aufs2-29
  cd ../linux-$_basekernel
  if [ "$_basekernel" != "$pkgver" ]; then
  # add latest kernel stable patch
         patch -Np1 < "${srcdir}"/patch-$pkgver ||return 1
  fi
  # add squashfs and aufs
  patch -Np1 -i "${srcdir}"/aufs2-standalone/aufs2-standalone.patch || return 1
  cp -R "${srcdir}"/aufs2-standalone/{Documentation,include,fs,config.mk} .
  echo "fix for sqlzma 3.4"
  patch -Np1 -i "${srcdir}"/squashfs3.4-include.patch || return 1
  # add injection support
  echo "applying zd1211rw injection patch"
  patch -Np0 -i "${srcdir}"/zd1211rw_inject_2.6.26.patch || return 1
  echo "applying mac80211 fragmentation attack patch"
  patch -Np1 -i "${srcdir}"/mac80211_2.6.28-rc4-wl_frag+ack_v3.patch || return 1
  echo "applying mac80211 no ack retry patch"
  patch -Np1 -i "${srcdir}"/mac80211-2.6.29-fix-tx-ctl-no-ack-retry-count.patch || return 1
  echo "applying rtl8187 injection speed patch"
  patch -Np1 -i "${srcdir}"/rtl8187-mac80211-injection-speed-2.6.28-wl.patch || return 1
  echo "applying ath5k injection patches"
  patch -Np1 -i "${srcdir}"/4001_ath5k-injection-2.6.28.patch || return 1
  echo "applying ath5k crc patch"
  patch -Np1 -i "${srcdir}"/4007_ath5k-pass-failed-crc.patch || return 1
  echo "applying ath5k freq chaos patch"
  patch -Np1 -i "${srcdir}"/4008_ath5k-frequency-chaos-2.6.28.patch || return 1
  echo "applying reiser4 patch"
  patch -Np1 -i "${srcdir}"/reiser4-for-2.6.29.patch || return 1
  # add openlog patch
  patch -Np0 -i "${srcdir}"/openlog.patch || return 1
  cp ../config ./.config
  # build the full kernel version to use in pathnames
  . ./.config
  make prepare
  make modules_prepare
  ### next line is only needed for rc kernels
  _kernver="$pkgver"${CONFIG_LOCALVERSION}""
  install -D -m644 Makefile \
    ${pkgdir}/usr/src/linux-${_kernver}/Makefile
  install -D -m644 kernel/Makefile \
    ${pkgdir}/usr/src/linux-${_kernver}/kernel/Makefile
  install -D -m644 .config \
    ${pkgdir}/usr/src/linux-${_kernver}/.config
  mkdir -p ${pkgdir}/usr/src/linux-${_kernver}/include

  for i in acpi asm-{generic,x86} config linux math-emu media net pcmcia scsi sound video; do
    cp -a include/$i ${pkgdir}/usr/src/linux-${_kernver}/include/
  done

  # copy arch includes for external modules
  mkdir -p ${pkgdir}/usr/src/linux-${_kernver}/arch/x86
  cp -a arch/x86/include ${pkgdir}/usr/src/linux-${_kernver}/arch/x86/

  # copy files necessary for later builds, like nvidia and vmware
  cp Module.symvers ${pkgdir}/usr/src/linux-${_kernver}
  cp -a scripts ${pkgdir}/usr/src/linux-${_kernver}
  # fix permissions on scripts dir
  chmod og-w -R ${pkgdir}/usr/src/linux-${_kernver}/scripts
  #mkdir -p ${pkgdir}/usr/src/linux-${_kernver}/.tmp_versions
  mkdir -p ${pkgdir}/usr/src/linux-${_kernver}/arch/$KARCH/kernel

  cp arch/$KARCH/Makefile ${pkgdir}/usr/src/linux-${_kernver}/arch/$KARCH/
  if [ "$CARCH" = "i686" ]; then
    cp arch/$KARCH/Makefile_32.cpu ${pkgdir}/usr/src/linux-${_kernver}/arch/$KARCH/
  fi
  cp arch/$KARCH/kernel/asm-offsets.s ${pkgdir}/usr/src/linux-${_kernver}/arch/$KARCH/kernel/

  # add headers for lirc package
  mkdir -p ${pkgdir}/usr/src/linux-${_kernver}/drivers/media/video
  cp drivers/media/video/*.h  ${pkgdir}/usr/src/linux-${_kernver}/drivers/media/video/
  for i in bt8xx cpia2 cx25840 cx88 em28xx et61x251 pwc saa7134 sn9c102 usbvideo zc0301; do
   mkdir -p ${pkgdir}/usr/src/linux-${_kernver}/drivers/media/video/$i
   cp -a drivers/media/video/$i/*.h ${pkgdir}/usr/src/linux-${_kernver}/drivers/media/video/$i
  done
  # add dm headers
  mkdir -p ${pkgdir}/usr/src/linux-${_kernver}/drivers/md
  cp drivers/md/*.h  ${pkgdir}/usr/src/linux-${_kernver}/drivers/md
  # add inotify.h
  mkdir -p ${pkgdir}/usr/src/linux-${_kernver}/include/linux
  cp include/linux/inotify.h ${pkgdir}/usr/src/linux-${_kernver}/include/linux/
  # add CLUSTERIP file for iptables
  mkdir -p ${pkgdir}/usr/src/linux-${_kernver}/net/ipv4/netfilter/
  # add wireless headers
  mkdir -p ${pkgdir}/usr/src/linux-${_kernver}/net/mac80211/
  cp net/mac80211/*.h ${pkgdir}/usr/src/linux-${_kernver}/net/mac80211/
  # add dvb headers for external modules
  # in reference to:
  # http://bugs.archlinux.org/task/9912
  mkdir -p ${pkgdir}/usr/src/linux-${_kernver}/drivers/media/dvb/dvb-core
  cp drivers/media/dvb/dvb-core/*.h ${pkgdir}/usr/src/linux-${_kernver}/drivers/media/dvb/dvb-core/
  # add dvb headers for external modules
  # in reference to:
  # http://bugs.archlinux.org/task/11194
  mkdir -p ${pkgdir}/usr/src/linux-${_kernver}/include/config/dvb/
  cp include/config/dvb/*.h ${pkgdir}/usr/src/linux-${_kernver}/include/config/dvb/
  # add dvb headers for http://mcentral.de/hg/~mrec/em28xx-new
  # in reference to:
  # http://bugs.archlinux.org/task/13146
  mkdir -p ${pkgdir}/usr/src/linux-${_kernver}/drivers/media/dvb/frontends/
  cp drivers/media/dvb/frontends/lgdt330x.h ${pkgdir}/usr/src/linux-${_kernver}/drivers/media/dvb/frontends/
  cp drivers/media/video/msp3400-driver.h ${pkgdir}/usr/src/linux-${_kernver}/drivers/media/dvb/frontends/
  # add xfs and shmem for aufs building
  mkdir -p ${pkgdir}/usr/src/linux-${_kernver}/fs/xfs
  mkdir -p ${pkgdir}/usr/src/linux-${_kernver}/mm
  cp fs/xfs/xfs_sb.h ${pkgdir}/usr/src/linux-${_kernver}/fs/xfs/xfs_sb.h
  # add vmlinux
  cp vmlinux ${pkgdir}/usr/src/linux-${_kernver}
  # copy in Kconfig files
  for i in `find . -name "Kconfig*"`; do 
    mkdir -p ${pkgdir}/usr/src/linux-${_kernver}/`echo $i | sed 's|/Kconfig.*||'`
    cp $i ${pkgdir}/usr/src/linux-${_kernver}/$i
  done
  cd ${pkgdir}/usr/src/linux-${_kernver}/include && ln -s asm-$KARCH asm
  chown -R root.root ${pkgdir}/usr/src/linux-${_kernver}
  find ${pkgdir}/usr/src/linux-${_kernver} -type d -exec chmod 755 {} \;
  # remove unneeded architectures
  rm -rf ${pkgdir}/usr/src/linux-${_kernver}/arch/{alpha,arm,arm26,avr32,blackfin,cris,frv,h8300,ia64,m32r,m68k,m68knommu,mips,mn10300,parisc,powerpc,ppc,s390,sh,sh64,sparc,sparc64,um,v850,xtensa}
}
