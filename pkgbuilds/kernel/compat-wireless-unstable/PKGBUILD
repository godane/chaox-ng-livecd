# Contributor: Jens Pranaitis <jens@chaox.net>
source "${startdir}"/../kernel.env
pkgname=compat-wireless-unstable
pkgver=2009_07_09
pkgrel=1
pkgdesc="Backport of current wireless drivers"
arch=(i686 x86_64)
url="http://wireless.kernel.org/en/users/Download/stable"
license=('GPL')
depends=("kernel26-chaox=$kernpkgver" "bash")
conflicts=("compat-wireless")
provides=("kernel26-chaox-wireless")
install=$pkgname.install
source=("http://www.orbit-lab.org/kernel/compat-wireless-2.6/2009/07/compat-wireless-2009-07-09.tar.bz2"
        # patches with kind permission from pentoo.ch
        "4000_mac80211_2.6.28-rc4-wl_frag+ack_v3.patch"
        "4002_mac80211-2.6.29-fix-tx-ctl-no-ack-retry-count.patch"
        "4004_zd1211rw-2.6.28.patch"
        "rtl8187_injection_speed_2.6.30-rc6.patch" # based on the aircrack-ng patch for -rc3
        )
md5sums=('ce404769bb3581ce8949142fa5b5ba42'
         'ca85d39144f07b0500c663779dda9259'
         'a713e50e8f2cf82ae9cf7e10a9f82412'
         '9c474a07e0f6f7b7549fa562e8cee080'
         'ff33e6f2120119707bc5de62b26947dd')
build()
{
  cd "${srcdir}"/${pkgname/-unstable/}-2009-07-09
  patch -Np1 -i "$srcdir"/4000_mac80211_2.6.28-rc4-wl_frag+ack_v3.patch || return 1
  patch -Np1 -i "$srcdir"/4002_mac80211-2.6.29-fix-tx-ctl-no-ack-retry-count.patch || return 1
  patch -Np1 -i "$srcdir"/4004_zd1211rw-2.6.28.patch || return 1
  patch -Np1 -i "$srcdir"/rtl8187_injection_speed_2.6.30-rc6.patch || return 1
  KLIB="/lib/modules/$_kernver" KLIB_BUILD="$_kerndir" make -j1 || return 1
  # Workaround for fscked up Makefile, idea stolen from pentoo as usual ;)
  for module in $(find . -name "*.ko")
  do
    install -D -m 644 "${module}" "${pkgdir}"/lib/modules/$_kernver/updates/${module} || return 1
  done
  install -D -m 644 scripts/modlib.sh "${pkgdir}"/usr/lib/compat-wireless/modlib.sh || return 1
  install -D -m 755 scripts/madwifi-unload "${pkgdir}"/usr/sbin/madwifi-unload || return 1
  for x in scripts/{*load,*enable}
  do
    install -m 755 $x "${pkgdir}"/usr/sbin/ || return 1
  done
  sed -i -e "s/_kernver='.*'/_kernver='${_kernver}'/" $startdir/*.install
}
