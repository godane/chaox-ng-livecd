# Contributor: Jens Pranaitis <jens@jenux.homelinux.org>
# Based on the work of godane <slaxemulator@gmail.com>
pkgname=kernel26-chaox
_basekernel=2.6.29
pkgver=2.6.29
pkgrel=3
pkgdesc="Linux kernel based on slax config. Used in chaox-ng"
arch=("i686")
license=('GPL2')
groups=('base')
url="http://www.kernel.org"
makedepends=("git")
depends=('coreutils' 'module-init-tools' 'mkinitcpio>=0.5.18')
conflicts=('aufs')
replaces=('kernel24' 'kernel24-scsi' 'kernel26-scsi'
          'alsa-driver' 'ieee80211' 'hostap-driver26'
          'pwc' 'nforce' 'squashfs' 'ivtv' 'aufs'
	  'zd1211' 'kvm-modules' 'iwlwifi' 'rt2x00-cvs' 'kernel26' 'kernel26slax')
provides=('kernel26')
install=$pkgname.install
source=(ftp://ftp.kernel.org/pub/linux/kernel/v2.6/linux-$_basekernel.tar.bz2
	http://kernel.org/pub/linux/kernel/v2.6/patch-$pkgver.bz2
        config
	http://patches.aircrack-ng.org/mac80211_2.6.28-rc4-wl_frag+ack_v3.patch
	http://patches.aircrack-ng.org/ath5k-injection-2.6.27-rc2.patch
	openlog.patch
	http://patches.aircrack-ng.org/zd1211rw_inject_2.6.26.patch
  squashfs3.4-include.patch
)
md5sums=('64921b5ff5cdadbccfcd3820f03be7d8'
         '1764da3e3f5fe588e1c72376c60b47c1'
         '588870da9989a502923d92170d217a45'
         'ca85d39144f07b0500c663779dda9259'
         '69c34c83c6f64570efa3bc65c39304c5'
         'a49390dd08e4f7702b8f4b550f1ad84e'
         'a162a23b5db02738309e54b6ca9fcbc9'
         '2ed00248c56672a6664864084011e85a')
build() {
  KARCH=x86
  cd "${srcdir}"
  git clone http://git.c3sl.ufpr.br/pub/scm/aufs/aufs2-standalone.git
  cd aufs2-standalone
  git checkout origin/aufs2-29
  cd ../linux-$_basekernel
  if [ "$_basekernel" != "$pkgver" ]; then
  # add latest kernel stable patch
         patch -Np1 < "${srcdir}"/patch-$pkgver ||return 1
  fi
  # add squashfs and aufs
  patch -Np1 -i "${srcdir}"/aufs2-standalone/aufs2-standalone.patch || return 1
  cp -R "${srcdir}"/aufs2-standalone/{Documentation,include,fs,config.mk} .
  echo "fix for sqlzma 3.4"
  patch -Np1 -i "${srcdir}"/squashfs3.4-include.patch || return 1
  # add injection support
  echo "applying zd1211rw injection patch"
  patch -Np0 -i "${srcdir}"/zd1211rw_inject_2.6.26.patch || return 1
  echo "applying mac80211 fragmentation attack patch"
  patch -Np1 -i "${srcdir}"/mac80211_2.6.28-rc4-wl_frag+ack_v3.patch || return 1
  echo "applying ath5k injection patch"
  patch -Np1 -i "${srcdir}"/ath5k-injection-2.6.27-rc2.patch || return 1
  # add openlog patch
  patch -Np0 -i "${srcdir}"/openlog.patch || return 1
  cp ../config ./.config
  # build the full kernel version to use in pathnames
  . ./.config
  ### next line is only needed for rc kernels
  _kernver="$pkgver"${CONFIG_LOCALVERSION}""
  # load configuration
  make menuconfig
  make bzImage modules || return 1
  mkdir -p "${pkgdir}"/{lib/modules,boot}
  make INSTALL_MOD_PATH="${pkgdir}" modules_install || return 1
  cp System.map "${pkgdir}"/boot/System.map26
  cp arch/$KARCH/boot/bzImage "${pkgdir}"/boot/vmlinuz26
  cd ${pkgdir}/lib/modules/${_kernver}
  rm -f source build; ln -sf ../../../usr/src/linux-${_kernver} build
  # set correct depmod command for install
  sed -i -e "s/KERNEL_VERSION=.*/KERNEL_VERSION=${_kernver}/g" "${startdir}"/$pkgname.install
  mkdir -p "${pkgdir}"/etc/mkinitcpio.d/
  echo -e "# DO NOT EDIT THIS FILE\nALL_kver='${_kernver}'" > "${pkgdir}"/etc/mkinitcpio.d/${pkgname}.kver
}
