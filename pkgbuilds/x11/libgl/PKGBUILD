# Contributor: Jens Pranaitis <jens@chaox.net>
# Contributor: Alexander Baldeck <kth5@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
pkgname=libgl
pkgver=7.4.2
pkgrel=5
pkgdesc="Mesa DRI OpenGL library and drivers"
arch=(i686 x86_64)
license=('LGPL')
url="http://www.mesa3d.org"
depends=('libdrm>=2.4.9' 'libxxf86vm' 'libxdamage' 'expat>=2.0.1')
makedepends=('dri2proto>=2.0' 'glproto>=1.4.9' 'pkgconfig')
provides=('libgl-dri')
replaces=('libgl-dri' 'libgl-mesa')
options=(!makeflags)
source=(http://downloads.sourceforge.net/mesa3d/MesaLib-${pkgver}.tar.bz2
	mesa-7.1-link-shared.patch
	7.4-branch.patch
  xorg-symlinks.sh)
md5sums=('b10a76e32bde4645cfc34ea0416d7d8b'
         'f0baa948d9810f268413111ee439d24b'
         '293e975b548b8cb29b23b735a112bafa'
         'f04bb92b6d6b8e66b2a1c2ef2c6516ef')
build() {
  cd "${startdir}/src/Mesa-${pkgver}"
  patch -Np1 -i "${srcdir}/mesa-7.1-link-shared.patch" || return 1
  patch -Np1 -i "${srcdir}/7.4-branch.patch" || return 1
  ./configure --prefix=/usr \
    --with-dri-driverdir=/usr/lib/xorg/modules/dri \
    --with-dri-drivers=swrast \
    --enable-glx-tls \
    --with-driver=dri \
    --enable-xcb \
    --disable-glu \
    --disable-glut \
    --disable-glw || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1
  mv "${pkgdir}"/usr/lib/libGL* "${pkgdir}"/usr/lib/xorg/
  rm -rf "${pkgdir}/usr/include"
  rm -rf "${pkgdir}/usr/lib/pkgconfig"
  install -m755 -d "${pkgdir}/usr/lib/xorg/modules/_extensions"
  mkdir "${pkgdir}"/usr/lib/xorg/modules/extensions
  ln -sf libglx.xorg ${pkgdir}/usr/lib/xorg/modules/_extensions/libglx.so || return 1
  install -D -m644 "${srcdir}"/xorg-symlinks.sh "${pkgdir}"/usr/share/select-opengl/functions/xorg-symlinks.sh
}
