# Contributor: Jens Pranaitis <jens@jenux.homelinux.org>
# Contributor: Thomas Baechler <thomas@archlinux.org>
# Contributor: James Rayner <iphitus@gmail.com>
pkgbase=nvidia-173xx
pkgname=("nvidia-173xx-utils" "nvidia-173xx-drivers")
pkgver=173.14.18
pkgrel=3
pkgdesc="NVIDIA legacy drivers utilities and libraries. 173xx branch."
arch=('i686')
[ "$CARCH" = "i686"   ] && ARCH=x86 	 
url="http://www.nvidia.com/"
license=('custom')
install=nvidia.install
source=(ftp://download.nvidia.com/XFree86/Linux-${ARCH}/${pkgver}/NVIDIA-Linux-${ARCH}-${pkgver}-pkg0.run nvidia-173xx-symlinks.sh)
md5sums=('480c10f3d861fd2c3cc2a46bccc961a4'
         '5ab459a41da54b8a64b078b97ec4bc7a')         
options=(docs !strip)
build()
{
  # override nvida install routine and do it the long way.
  cd ${srcdir}/
  sh NVIDIA-Linux-${ARCH}-${pkgver}-pkg0.run --extract-only
}
package_nvidia-173xx-drivers() {
depends=("gcc-libs" "zlib" "libxext" "xorg-server")
provides=("libgl")
  cd "${srcdir}"/NVIDIA-Linux-${ARCH}-${pkgver}-pkg0/usr
  mkdir -p ${pkgdir}/usr/lib/nvidia-173xx/modules/{extensions,drivers}
  install lib/{libGLcore,libGL,libnvidia-cfg,libcuda,tls/libnvidia-tls}.so.${pkgver} \
       ${pkgdir}/usr/lib/nvidia-173xx/ || return 1
  install X11R6/lib/libXv* ${pkgdir}/usr/lib/nvidia-173xx/ || return 1
  install X11R6/lib/modules/drivers/nvidia_drv.so ${pkgdir}/usr/lib/nvidia-173xx/modules/drivers || return 1
  install X11R6/lib/modules/extensions/libglx.so.$pkgver ${pkgdir}/usr/lib/nvidia-173xx/modules/extensions || return 1
  cd ${pkgdir}/usr/lib/nvidia-173xx/
  ln -s libGL.so.$pkgver libGL.so || return 1
  ln -s libGL.so.$pkgver libGL.so.1 || return 1
  ln -s libGLcore.so.$pkgver libGLcore.so.1 || return 1
  ln -s libnvidia-cfg.so.$pkgver libnvidia-cfg.so.1 || return 1
  ln -s libnvidia-tls.so.$pkgver libnvidia-tls.so.1 || return 1
  ln -s libcuda.so.$pkgver libcuda.so.1 || return 1
  ln -s libXvMCNVIDIA.so.$pkgver libXvMCNVIDIA_dynamic.so.1 || return 1
  cd ${pkgdir}/usr/lib/nvidia-173xx/modules/extensions
  ln -s libglx.so.$pkgver libglx.so || return 1
  find ${pkgdir}/usr -type d -exec chmod 755 {} \;
  install -D -m644 "${srcdir}"/nvidia-173xx-symlinks.sh "${pkgdir}"/usr/share/select-opengl/functions/nvidia-173xx-symlinks.sh
  # phew :)
}
package_nvidia-173xx-utils() {
conflicts=("nvidia-utils")
depends=("gtk2")
  cd "${srcdir}"/NVIDIA-Linux-${ARCH}-${pkgver}-pkg0/usr
  mkdir -p "${pkgdir}"/usr/{bin,share/applications,share/man/man1,share/pixmaps/}
  install -m755 bin/nvidia-{settings,xconfig,bug-report.sh} $pkgdir/usr/bin/ || return 1
  install -m644 share/applications/nvidia-settings.desktop $pkgdir/usr/share/applications/ || return 1
  sed -e 's:__UTILS_PATH__:/usr/bin:' -e 's:__PIXMAP_PATH__:/usr/share/pixmaps:' -i $pkgdir/usr/share/applications/nvidia-settings.desktop
  install -m644 share/pixmaps/nvidia-settings.png $pkgdir/usr/share/pixmaps/ || return 1
  install -m644 share/man/man1/* $pkgdir/usr/share/man/man1/ || return 1
  rm $pkgdir/usr/share/man/man1/nvidia-installer.1.gz || return 1
}
