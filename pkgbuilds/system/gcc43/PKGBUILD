# Contributor: Edgar Hucek <gimli at dark-green dot com>

pkgname=gcc43
_ver=4.3
pkgver=4.3.3
pkgrel=1
pkgdesc="The GNU Compiler Collection"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL')
url="http://gcc.gnu.org"
depends=('glibc' 'binutils' 'gmp' 'mpfr' 'ppl' 'cloog-ppl')
makedepends=('flex' 'bison')
options=('!libtool')
source=(ftp://gcc.gnu.org/pub/gcc/releases/gcc-${pkgver}/gcc-{core,g++,fortran,objc,java}-${pkgver}.tar.bz2
	gcc_pure64.patch
	gcc-hash-style-both.patch
	buildfix_new_bison.patch)
md5sums=('1739288c2c7b1472796b33d641dbdbbd'
         '18428e313a9927d38b313e688c62219b'
         '8771e6190dd4f3178b2a7978d2380a5d'
         '125b75947c763ba59b7ea95d3739d431'
         '068938366ee0be5b70112bdf485898b4'
         '4030ee1c08dd1e843c0225b772360e76'
         'bb420bc84b1104455b7230b1cd4b96c2'
         '6895f128e905bfcb26e328899235e553')

build() {
  if ! locale -a | grep ^de_DE; then
          echo "You need the de_DE locale to build gcc."
    return 1
  fi

  cd ${srcdir}/gcc-${pkgver}
  # Don't install libiberty
  sed -i 's/install_to_$(INSTALL_DEST) //' libiberty/Makefile.in

  if [ "${CARCH}" = "x86_64" ]; then
    patch -Np1 -i ../gcc_pure64.patch || return 1
  fi
  patch -Np0 -i ${srcdir}/gcc-hash-style-both.patch || return 1
  patch -Np0 -i ${srcdir}/buildfix_new_bison.patch || return 1

  echo ${pkgver} > gcc/BASE-VER

  # Don't run fixincludes
  sed -i -e 's@\./fixinc\.sh@-c true@' gcc/Makefile.in

  mkdir build
  cd build
  ../configure --prefix=/usr --enable-shared \
      --enable-languages=c,c++ \
      --enable-threads=posix --mandir=/usr/share/man --infodir=/usr/share/info \
      --enable-__cxa_atexit  --disable-multilib --libdir=/usr/lib \
      --libexecdir=/usr/lib --enable-clocale=gnu --disable-libstdcxx-pch \
      --with-tune=generic \
      --disable-werror --enable-checking=release \
      --program-suffix=-${_ver} --enable-version-specific-runtime-libs
  make || return 1
  make -j1 DESTDIR=${pkgdir} install || return 1

  # Lazy way of dealing with conflicting man and info pages...
  rm -rf ${pkgdir}/usr/share
}
